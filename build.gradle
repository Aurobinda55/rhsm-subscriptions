plugins {
    id "checkstyle"
    id "org.springframework.boot" version "2.0.8.RELEASE"
}

allprojects {
    apply plugin: 'java'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    repositories {
        mavenCentral()
    }

    ext {
        swagger_annotations_version = "1.5.21"
        jackson_version = "2.8.11"
        jackson_databind_version = "2.8.11.2"
        resteasy_version = "3.6.2.Final"
        spring_version = '5.1.4.RELEASE'
        spring_boot_version = '2.1.2.RELEASE'
        junit_version = '5.3.2'
    }
}

checkstyle {
    toolVersion '8.16'
}

version = '1.0.0'

bootJar {
}

processResources {
    expand(project.properties)
}
ext {

}

dependencies {
    compile project('api')
    compile project('insights-inventory-client')
    compile project('pinhead-client')
    compile "javax.servlet:javax.servlet-api:3.1.0"
    compile "org.jboss.spec.javax.ws.rs:jboss-jaxrs-api_2.1_spec:1.0.2.Final"
    compile "org.springframework.boot:spring-boot-autoconfigure:$spring_boot_version"
    compile "org.springframework.boot:spring-boot:$spring_boot_version"
    compile "org.springframework:spring-beans:$spring_version"
    compile "org.springframework:spring-context:$spring_version"
    compile "org.slf4j:slf4j-api:1.7.25"
    compile "net.logstash.logback:logstash-logback-encoder:5.3"

    // Runtime deps that will be included in the result package but not on the compile classpath.  I.e.
    // implementations of APIs we are using.
    runtime "ch.qos.logback:logback-classic:1.2.3"
    runtime "org.jboss.resteasy:resteasy-spring-boot-starter:3.0.0.Final"
    runtime "org.jboss.resteasy:resteasy-jaxrs:$resteasy_version"
    runtime "org.jboss.resteasy:resteasy-validator-provider-11:$resteasy_version"
    runtime "org.hibernate.validator:hibernate-validator:6.0.14.Final"

    testCompile "org.springframework.boot:spring-boot-test:$spring_boot_version"
    testCompile "org.springframework:spring-test:$spring_version"
    testCompile "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_version"

//    testCompile 'org.mockito:mockito-core:2.23.4'
//    testCompile 'org.mockito:mockito-junit-jupiter:2.23.4'
}

test {
    useJUnitPlatform()
}

configure(subprojects.findAll { it.name == 'insights-inventory-client' || it.name == 'pinhead-client'}) {
    dependencies {
        compile "io.swagger:swagger-annotations:$swagger_annotations_version"
        compile "org.jboss.resteasy:resteasy-client:$resteasy_version"
        compile "org.jboss.resteasy:resteasy-multipart-provider:$resteasy_version"
        compile "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
        compile "com.fasterxml.jackson.core:jackson-databind:$jackson_databind_version"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    }

    sourceSets {
        main {
            java.srcDirs = ["${buildDir}/generated/src/main/java"]
        }
    }

    checkstyle.sourceSets = []
}
